buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.moowork.node"

group = 'quiz'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8


node {
	version = '9.8.0'
	npmVersion = '5.6.0'
	download = true
}

repositories {
	maven { url "http://repo.maven.apache.org/maven2" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-freemarker')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile 'org.projectlombok:lombok:1.16.16'
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
	def osName = System.getProperty("os.name").toLowerCase();
	if (osName.contains("windows")) {
		script = project.file('node_modules/webpack/bin/webpack.js')
	} else {
		script = project.file('node_modules/.bin/webpack')
	}
}

processResources.dependsOn 'webpack'

clean.delete << file('node_modules')
